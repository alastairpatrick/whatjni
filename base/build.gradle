import whatjni.util.FindVMLibrary

plugins {
    id 'cpp-library'
    id 'cpp-unit-test'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(project(":googletest"))
}

library {
    linkage = [Linkage.STATIC, Linkage.SHARED]

    targetMachines = [
            machines.linux.x86_64,
            machines.windows.x86, machines.windows.x86_64,
            machines.macOS.x86_64
    ]
}

unitTest {
    targetMachines = [
            machines.linux.x86_64,
            machines.windows.x86, machines.windows.x86_64,
            machines.macOS.x86_64
    ]
}

tasks.withType(CppCompile).configureEach {
    macros.put("WHATJNI_BASE_EXPORT", null)
}

tasks.withType(LinkExecutable).configureEach {
    linkerArgs.addAll targetPlatform.map { targetPlatform ->
        if (targetPlatform.operatingSystem.isLinux()) {
            return ['-ldl', '-lpthread']
        }
        return []
    }
}

tasks.withType(RunTestExecutable).configureEach {
    environment.put("JVM_LIBRARY_PATH", FindVMLibrary.find())
}
