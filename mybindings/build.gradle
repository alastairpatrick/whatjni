import whatjni.GenJNIBindingsTask

plugins {
    id 'cpp-application'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    jniBinding {
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, "java-api"))
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, "classes"))
        }
    }
}

dependencies {
    implementation(project(":base"))
    jniBinding "org.ow2.asm:asm:9.1"
    jniBinding(project(":mymodule"))
}

application {
    targetMachines = [
            machines.linux.x86_64,
            machines.windows.x86, machines.windows.x86_64,
            machines.macOS.x86_64
    ]
}

task genBindings(type: GenJNIBindingsTask) {
    classpath.from configurations.jniBinding.files
}

tasks.withType(CppCompile).configureEach {
    dependsOn "genBindings"
    includes.from("$buildDir/generated/sources/jniBindings")
}
