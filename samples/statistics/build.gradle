import whatjni.util.FindVMLibrary
import whatjni.GenJNIBindingsTask

plugins {
    id 'cpp-application'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    jniBinding {
        canBeConsumed = false
        canBeResolved = true

        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, "java-api"))
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, "classes"))
        }
    }
}

dependencies {
    implementation(project(":base"))
    jniBinding "org.apache.commons:commons-math3:3.6.1"
}

application {
    targetMachines = [
            machines.linux.x86_64,
            machines.windows.x86, machines.windows.x86_64,
            machines.macOS.x86_64
    ]
}

task genBindings(type: GenJNIBindingsTask) {
    dependsOn configurations.jniBinding
    classpath.from configurations.jniBinding.files
    nativePackages.addAll(["java.lang"])
}

tasks.withType(CppCompile).configureEach {
    dependsOn "genBindings"
    includes.from("$buildDir/generated/sources/jniBindings")
}

tasks.withType(InstallExecutable).all { installTask ->
    def task = tasks.register("whatjni\$${installTask.name}") {
        doLast {
            def scriptPath = installTask.installDirectory.file("whatjni.bat").get().asFile
            def classpath = configurations.jniBinding.asPath
            def vmPath = whatjni.util.FindVMLibrary.find()
            def executableFileName = installTask.executableFile.asFile.get().name
            scriptPath.parentFile.mkdirs()
            scriptPath.withWriter {
                it.writeLine("@echo off")
                it.writeLine("set \"WHATJNI_CLASSPATH=$classpath\"")
                it.writeLine("set \"WHATJNI_VM_PATH=$vmPath\"")
                it.writeLine("call \"%~dp0lib\\$executableFileName\" %*")
                it.writeLine("exit /B %ERRORLEVEL%")
            }
        }
    }

    installTask.dependsOn(task)
}
